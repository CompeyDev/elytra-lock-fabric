plugins {
	id 'fabric-loom' version '1.10-SNAPSHOT'
	id "com.modrinth.minotaur" version "2.+"
	id 'maven-publish'
}

version = "${project.mod_version}+${project.minecraft_version}"
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	mavenCentral()
	maven {
		// For ImageIO
		name "SonaType Maven"
		url "https://oss.sonatype.org/content/repositories/snapshots"
	}

	maven {
		// For YACL
		name "Xander Maven"
		url "https://maven.isxander.dev/releases"
	}

	maven {
		// For modmenu
		name "TerraformersMC Maven"
		url "https://maven.terraformersmc.com/releases"
	}
}

loom {
	mods {
		"elytra-lock" {
			sourceSet sourceSets.main
		}
	}

}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modImplementation "dev.isxander:yet-another-config-lib:${project.yacl_version}"
	modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"
}

processResources {
	inputs.property "version", project.version
	filesMatching("fabric.mod.json") {
		expand "version": project.version,
				   "java_version": ">=21",
				   "minecraft_version": "~${project.minecraft_version}",
				   "loader_version": ">=${project.loader_version}"
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "elytralock"
    versionType = "release"
	debugMode = false 
    uploadFile = remapJar
    loaders = ["fabric", "quilt"]
	// syncBodyFrom = rootProject.file("README.md").text
    dependencies {
        required.project "fabric-api"
        optional.version "modmenu", "${project.modmenu_version}"
		optional.version "yacl", "${project.yacl_version}"
    }
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	repositories {}
}
